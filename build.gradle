
project.ext {
    SPINE_VERSION = '0.9.36-SNAPSHOT'
    BASE_VERSION = '0.9.35-SNAPSHOT'

    publishPlugin = "$rootDir/scripts/publish.gradle"
    credentialsPropertyFile = 'credentials.properties'
    projectsToPublish = ['rdbms']

    guavaVersion = '20.0'
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'jacoco'

    group = 'io.spine'
    version = SPINE_VERSION
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'


    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    // Set Java home to point to JDK7 in gradle.properties file.
    //
    // For Mac OS X, it looks like this:
    //
    // # suppress inspection "UnusedProperty"
    // org.gradle.java.home=/Library/Java/JavaVirtualMachines/jdk1.7.0_80.jdk/Contents/Home/

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'http://maven.teamdev.com/repository/spine' }
        maven { url = 'http://maven.teamdev.com/repository/spine-snapshots' }
    }

    dependencies {

        // Exclude transient dependencies from Spine
        final def excludes = {
            exclude group: 'com.google.protobuf', module: 'protobuf-lite'
            exclude group: 'io.grpc'
        }

        // Spine
        compile group: 'io.spine', name: 'spine-base', version: project.BASE_VERSION, changing: true, excludes
        compile group: 'io.spine', name: 'spine-core', version: project.SPINE_VERSION, changing: true, excludes
        compile group: 'io.spine', name: 'spine-client', version: project.SPINE_VERSION, changing: true, excludes
        compile group: 'io.spine', name: 'spine-server', version: project.SPINE_VERSION, changing: true, excludes


        testCompile group: 'io.spine', name: 'spine-server', version: project.SPINE_VERSION, changing: true, classifier: 'test', excludes
        testCompile group: 'io.spine', name: 'spine-client', version: project.SPINE_VERSION, changing: true, classifier: 'test', excludes

        compile group: 'io.spine', name: 'spine-testutil-base', version: project.BASE_VERSION, changing: true, excludes
        compile group: 'io.spine', name: 'spine-testutil-client', version: project.SPINE_VERSION, changing: true, excludes
        compile group: 'io.spine', name: 'spine-testutil-server', version: project.SPINE_VERSION, changing: true, excludes

        // Guava
        compile group: 'com.google.guava', name: 'guava', version: project.guavaVersion

        // Findbugs
        compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

        // SLF4J
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
        compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.21'

        // JUnit
        testCompile(group: 'junit', name: 'junit', version: '4.12') {
            exclude(module: 'hamcrest-core')
        }
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile 'org.mockito:mockito-core:2.+'
        testCompile group: 'com.google.guava', name: 'guava-testlib', version: project.guavaVersion
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }

    task testOutputJar(type: Jar) {
        from sourceSets.test.output
        classifier "test"
    }

    check.dependsOn jacocoTestReport
}

apply from: publishPlugin
