/*
 * Copyright 2019, TeamDev. All rights reserved.
 *
 * Redistribution and use in source and/or binary forms, with or without
 * modification, must retain the above copyright notice and the following
 * disclaimer.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

buildscript { final scriptHandler ->
    // Define the versions of the library and its dependencies.
    // As long as `buildscript` section is always evaluated first, we need to apply
    // `version.gradle` explicitly here.
    apply from: "$rootDir/config/gradle/dependencies.gradle"
    apply from: "$rootDir/version.gradle"


    defaultRepositories(scriptHandler)
    dependencies {
        classpath deps.build.gradlePlugins.errorProne
    }

    forceConfiguration(scriptHandler)

}

ext {
    credentialsPropertyFile = 'credentials.properties'
    projectsToPublish = ['rdbms']
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'jacoco'

    group = 'io.spine'
    version = versionToPublish
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'net.ltgt.errorprone'
    apply plugin: 'pmd'
    apply plugin: 'maven-publish'
    apply from: deps.scripts.javadocOptions
    apply from: deps.scripts.projectLicenseReport

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    defaultRepositories(project)
    forceConfiguration(project)

    dependencies {

        // Exclude transient dependencies from Spine
        final def excludes = {
            exclude group: 'com.google.protobuf', module: 'protobuf-lite'
            exclude group: 'io.grpc'
        }

        // Spine
        implementation group: 'io.spine', name: 'spine-server', version: spineCoreVersion, excludes

        implementation group: 'io.spine', name: 'spine-testutil-server', version: spineCoreVersion,
                excludes

        testImplementation group: 'io.spine', name: 'spine-server', version: spineCoreVersion,
                classifier: 'test', excludes

        testImplementation group: 'io.spine', name: 'spine-testlib', version: spineBaseVersion
        testImplementation "io.spine.tools:spine-mute-logging:$spineBaseVersion"

        errorprone deps.build.errorProneCore
        errorproneJavac deps.build.errorProneJavac
        implementation deps.build.guava
        implementation deps.build.jsr305Annotations
        implementation deps.build.checkerAnnotations
        implementation deps.build.errorProneAnnotations
        implementation deps.build.slf4j

        testImplementation deps.test.junit5Api
        testImplementation deps.test.junit5Runner
        testImplementation deps.test.hamcrest
        testImplementation deps.test.mockito
        testImplementation deps.test.guavaTestlib
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }

    task testOutputJar(type: Jar) {
        from sourceSets.test.output
        classifier "test"
    }

    task javadocJar(type: Jar, dependsOn: 'javadoc') {
        from("$projectDir/build/docs/javadoc")
        classifier "javadoc"
    }

    check.dependsOn jacocoTestReport

    apply from: deps.scripts.javacArgs
    apply from: deps.scripts.pmd
}

apply from: deps.scripts.jacoco
apply from: deps.scripts.publish
apply from: deps.scripts.repoLicenseReport
apply from: deps.scripts.generatePom
