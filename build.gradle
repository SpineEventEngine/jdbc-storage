
project.ext {
    SPINE_VERSION = '0.7.0'

    publishPlugin = "$rootDir/scripts/publish.gradle"
    credentialsPropertyFile = 'credentials.properties'
    projectsToPublish = ['rdbms']
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'jacoco'

    group = 'org.spine3'
    version = SPINE_VERSION
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    // Set Java home to point to JDK7 in gradle.properties file.
    //
    // For Mac OS X, it looks like this:
    //
    // # suppress inspection "UnusedProperty"
    // org.gradle.java.home=/Library/Java/JavaVirtualMachines/jdk1.7.0_80.jdk/Contents/Home/

    repositories {
        mavenCentral()
        maven { url = 'http://maven.teamdev.com/repository/spine' }
        maven { url = 'http://maven.teamdev.com/repository/spine-snapshots' }
    }

    dependencies {
        // Spine
        compile group: 'org.spine3', name: 'spine-server-core', version: project.SPINE_VERSION, changing: true
        compile group: 'org.spine3', name: 'spine-client-core', version: project.SPINE_VERSION, changing: true
        compile group: 'org.spine3', name: 'spine-testutil-core', version: project.SPINE_VERSION, changing: true

        // Guava
        compile group: 'com.google.guava', name: 'guava', version: '19.0'

        // Findbugs
        compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

        // SLF4J
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
        compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.21'

        // JUnit
        testCompile(group: 'junit', name: 'junit', version: '4.12') {
            exclude(module: 'hamcrest-core')
        }
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile 'org.mockito:mockito-core:2.+'
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }

    task testOutputJar(type: Jar) {
        from sourceSets.test.output
        classifier "test"
    }

    check.dependsOn jacocoTestReport
}

apply from: publishPlugin
